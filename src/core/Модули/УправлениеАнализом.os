#Использовать 1commands

Функция ПолучитьВерсию(ПутьКФайлу)
	ТекстовыйФайл = Новый ТекстовыйДокумент;
	ТекстовыйФайл.Прочитать(ПутьКФайлу + "/src/cf/Configuration.xml", КодировкаТекста.UTF8, Символы.ВК + Символы.ПС);
	СтрокаВерсии = ВРег(ТекстовыйФайл.ПолучитьТекст());
	ПозицияСтарта = Найти(СтрокаВерсии, "<VERSION>") + 9;
	ПозицияОкончания = Найти(СтрокаВерсии, "</VERSION>");
	ЧислоСимволов = ПозицияОкончания - ПозицияСтарта;
	Версия = Сред(СтрокаВерсии, ПозицияСтарта, ЧислоСимволов);
	
	Возврат Версия;
КонецФункции

Функция ПолучитьКоммиты(КаталогРепозитария, ДатаПоследнегоАнализа)
	КомандаГит = Новый Команда();
	КомандаГит.УстановитьКоманду("git");
	КомандаГит.ДобавитьПараметр("log");
	КомандаГит.ДобавитьПараметр("--reverse");
	КомандаГит.ДобавитьПараметр("--pretty=format:""%h | %cd""");
	КомандаГит.ДобавитьПараметр("--date=format:%Y%m%d");
	КомандаГит.ДобавитьПараметр("--branches=""master*""");
	КомандаГит.УстановитьРабочийКаталог(КаталогРепозитария);
	
	КомандаГит.Исполнить();
	
	КоммитыСДатами = СтрРазделить(КомандаГит.ПолучитьВывод(), Символы.ПС, Ложь);
	Коммиты = Новый ТаблицаЗначений();
	Коммиты.Колонки.Добавить("Коммит");
	Коммиты.Колонки.Добавить("КоммитДата");
	
	ПоследняяДатаКоммита = Дата(СтрЗаменить(ДатаПоследнегоАнализа, "-", ""));
	
	Для Каждого КоммитСДатой Из КоммитыСДатами Цикл
		ТекКоммитСДатой = СтрРазделить(КоммитСДатой, "|");
		ДатаКоммитаСтрокой = СокрЛП(ТекКоммитСДатой[1]);
		ДатаКоммита = Дата(ДатаКоммитаСтрокой);
		Если  ДатаКоммита > ПоследняяДатаКоммита Тогда
			ПоследняяДатаКоммита = ДатаКоммита;
			СтрокаТЗ = Коммиты.Добавить();
			СтрокаТЗ.Коммит = СокрЛП(ТекКоммитСДатой[0]);
			СтрокаТЗ.КоммитДата = Формат(ДатаКоммита, "ДФ=""гггг-ММ-дд""");            
		КонецЕсли;
	КонецЦикла;
	
	Возврат Коммиты;
КонецФункции

Процедура Старт()
	АдресСонара = "http://localhost:9000/"; // адрес сервера sonarqube
	КлючПроекта = ""; // ключ проекта на sonarqube
	НаименованиеПроекта = "="; // название проекта на sonarqube
	КаталогРепозитария = ""; // /path/to/project
	ДатаПоследнегоСкана = ""; // например 2020-01-01
	ТокенСонара = ""; // токен в sonarqube
	
	Сообщить("Получаю список коммитов");
	
	КоммитыДляСканирования = ПолучитьКоммиты(КаталогРепозитария, ДатаПоследнегоСкана);
	КоличествоКоммитовКОбработке = КоммитыДляСканирования.Количество();
	Сообщить("Начинаю обработку коммитов. Всего к обработке : " + КоличествоКоммитовКОбработке);
	
	Для Индекс = 0 По КоличествоКоммитовКОбработке - 1 Цикл
		
		ТекКоммит = КоммитыДляСканирования[Индекс].Коммит;
		ТекКоммитДата = КоммитыДляСканирования[Индекс].КоммитДата;
		
		Сообщить("Переключаюсь на коммит : " + ТекКоммит + " от " + ТекКоммитДата);
		КомандаЧекаут = Новый Команда();
		КомандаЧекаут.УстановитьКоманду("git");
		КомандаЧекаут.ПоказыватьВыводНемедленно(Ложь);
		КомандаЧекаут.ДобавитьПараметр("checkout");
		КомандаЧекаут.ДобавитьПараметр(ТекКоммит);
		КомандаЧекаут.УстановитьРабочийКаталог(КаталогРепозитария);
		КомандаЧекаут.Исполнить();
		
		ВерсияВКоммите = ПолучитьВерсию(КаталогРепозитария);
		
		Сообщить("Запускаю сканирование : " + СокрЛП(Индекс + 1) + "/" + КоличествоКоммитовКОбработке);
		КомандаСканера = Новый Команда();
		КомандаСканера.УстановитьКоманду("D:/sonar-scanner/bin/sonar-scanner.bat");
		КомандаСканера.ПоказыватьВыводНемедленно(Ложь);
		КомандаСканера.ДобавитьПараметр("-Dsonar.login=" + ТокенСонара);
		КомандаСканера.ДобавитьПараметр("-Dsonar.host.url=" + АдресСонара);
		КомандаСканера.ДобавитьПараметр("-Dsonar.projectKey=" + КлючПроекта);
		КомандаСканера.ДобавитьПараметр("-Dsonar.projectName=" + НаименованиеПроекта);
		КомандаСканера.ДобавитьПараметр("-Dsonar.projectBaseDir=" + КаталогРепозитария);
		КомандаСканера.ДобавитьПараметр("-Dsonar.sources=" + КаталогРепозитария + "\src");
		КомандаСканера.ДобавитьПараметр("-Dsonar.sourceEncoding=UTF-8");
		КомандаСканера.ДобавитьПараметр("-Dsonar.inclusions=**/*.bsl");
		КомандаСканера.ДобавитьПараметр("-Dsonar.scm.enabled=true");
		КомандаСканера.ДобавитьПараметр("-Dsonar.scm.provider=git");
		КомандаСканера.ДобавитьПараметр("-Dsonar.scm.exclusions.disabled=true");
		КомандаСканера.ДобавитьПараметр("-Dsonar.projectDate=" + ТекКоммитДата);
		КомандаСканера.ДобавитьПараметр("-Dsonar.projectVersion=" + ВерсияВКоммите);
		КомандаСканера.Исполнить();
		
		Сообщить("Сканирование завешено ");      
		Сообщить(КомандаСканера.ПолучитьВывод());
		Сообщить("Коммит " + ТекКоммит + " от " + ТекКоммитДата + " обработан.");
		Сообщить("Осталось еще обработать : " + (КоличествоКоммитовКОбработке - Индекс - 1));
	КонецЦикла;
КонецПроцедуры